_realname=adawebpack
pkgbase=mingw-w64-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}
pkgver=22.1.0
pkgrel=1
pkgdesc="AdaWebPack is a toolchain, RTL and WebAPI binding for Ada on WebAssembly"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64')
url="https://github.com/godunko/adawebpack"
license=('GPL3' 'BSD3')
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc-ada"
             "${MINGW_PACKAGE_PREFIX}-gprbuild"
             "${MINGW_PACKAGE_PREFIX}-clang<15.0.0"
             "${MINGW_PACKAGE_PREFIX}-lld<15.0.0"
             "${MINGW_PACKAGE_PREFIX}-llvm<15.0.0")
depends=("${MINGW_PACKAGE_PREFIX}-llvm<15.0.0")
source=("gnat-llvm.zip::https://github.com/AdaCore/gnat-llvm/archive/28c91e94c4227e6d9eabb2aeed4c0c12f6a4f3de.zip"
        "bb-runtimes.zip::https://github.com/Fabien-Chouteau/bb-runtimes/archive/c1d089bd8880cec250266cf71d6a22cc74fdba79.zip"
        "gcc.zip::https://github.com/gcc-mirror/gcc/archive/64d5610f44c995b88261bf83f53a200355cb530f.zip"
        "${_realname}.tar.gz")
md5sums=( SKIP SKIP SKIP SKIP )
noextract=( "gcc.zip" )

prepare() {
  cd ${srcdir}/
  bsdtar -x -f gcc.zip gcc-*/gcc/ada
  mv -v gnat-llvm-* gnat-llvm
  mv -v gcc-*/gcc/ada gnat-llvm/llvm-interface/gnat_src
  mv -v bb-runtimes-*/gnat_rts_sources/include/rts-sources gnat-llvm/llvm-interface/
  mv -v adawebpack gnat-llvm/llvm-interface/adawebpack_src
  cd gnat-llvm
  patch -p0 < llvm-interface/adawebpack_src/patches/gnat-llvm.patch
  cd llvm-interface/
  cp -v adawebpack_src/source/rtl/Makefile.target .
  rm -rf gcc-*
  rm -rf bb-runtimes-*
}


build() {
  cd ${srcdir}/gnat-llvm/llvm-interface/
  make wasm
  export PATH=$PWD/bin:$PATH
  gprconfig --batch -o /tmp/llvm.cgpr --db `pwd`/adawebpack_src/packages/Fedora --target=llvm --config=ada,,
  gprbuild --target=llvm -P adawebpack_src/gnat/adawebpack.gpr -p --config=/tmp/llvm.cgpr
}

package() {
  cd ${srcdir}/gnat-llvm/llvm-interface/
  export PATH=$PWD/bin:$PATH
  PREFIX=${pkgdir}
  mkdir -p ${pkgdir}${MINGW_PREFIX}/share/gprconfig
  mkdir -p ${pkgdir}${MINGW_PREFIX}/lib/gnat
  mkdir -p ${pkgdir}${MINGW_PREFIX}/share/adawebpack
  cp adawebpack_src/packages/Fedora/llvm.xml ${pkgdir}${MINGW_PREFIX}/share/gprconfig/
  cp adawebpack_src/gnat/adawebpack_config.gpr ${pkgdir}${MINGW_PREFIX}/lib/gnat/
  cp -r bin lib ${pkgdir}${MINGW_PREFIX}
  cp -v adawebpack_src/source/adawebpack.mjs ${pkgdir}${MINGW_PREFIX}/share/adawebpack/
  gprinstall --target=llvm --prefix=${pkgdir}${MINGW_PREFIX} --project-subdir=${pkgdir}${MINGW_PREFIX}/lib/gnat -P adawebpack_src/gnat/adawebpack.gpr -p --config=/tmp/llvm.cgpr
}